; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Languages]
Name: fr; MessagesFile: compiler:French-15-4.1.8.isl

[Setup]
AppVersion={#GetFileVersion("G:\Programmation\MEDIA.KIT\WallPap 1.0\WPConf.dll")}
AppName=WallPepper {#SetupSetting("AppVersion")}
AppVerName=WallPepper {#SetupSetting("AppVersion")}
AppMutex=TetramCorpWallPepperMutex
AppPublisher=Teträm Corp
AppPublisherURL=http://www.tetram.info
AppSupportURL=mailto:dev@tetram.info
AppUpdatesURL=http://www.tetram.info
UninstallDisplayIcon={app}\WallPepper.exe
DefaultDirName={pf}\Teträm Corp\WallPepper
DefaultGroupName=Teträm Corp\WallPepper
PrivilegesRequired=poweruser
AppCopyright=Teträm Corp © 1997-2004
AppID={{5AED0247-C16B-4DFC-B7FD-42CCE72A4F89}
LicenseFile=G:\Programmation\MEDIA.KIT\LicenceFreeWare.rtf
WindowVisible=false
BackColor=$ffbfbf
BackColor2=clBlue
BackColorDirection=toptobottom
WizardImageBackColor=clBlack
OutputBaseFilename=WallPepper-setup-{#SetupSetting("AppVersion")}
MinVersion=4.1.1998,5.0.2195
EnableDirDoesntExistWarning=true
AllowUNCPath=false
AppendDefaultDirName=false
ShowLanguageDialog=yes
InternalCompressLevel=normal
SolidCompression=true
Compression=lzma/normal
VersionInfoVersion={#SetupSetting("AppVersion")}
VersionInfoCompany=Teträm Corp
VersionInfoTextVersion={#SetupSetting("AppVersion")}
WizardImageFile=G:\Programmation\MEDIA.KIT\WallPap 1.0\Setup\SetupModern23.bmp
WizardSmallImageFile=SetupModernSmall19.bmp

[Tasks]
Name: desktopicon; Description: Créer un raccourci sur le &bureau; GroupDescription: Raccourcis supplémentaires:
Name: interneticon; Description: Créer un raccourci vers le site de Teträm Corp; GroupDescription: Raccourcis supplémentaires:

[Files]
Source: G:\Programmation\MEDIA.KIT\LicenceFreeWare.rtf; DestDir: {app}
Source: G:\Programmation\MEDIA.KIT\WallPap 1.0\WallPepper.exe; DestDir: {app}
Source: G:\Programmation\MEDIA.KIT\WallPap 1.0\WPConf.dll; DestDir: {app}
Source: G:\Programmation\MEDIA.KIT\WallPap 1.0\unrar.dll; DestDir: {app}; Flags: ignoreversion
Source: G:\Programmation\MEDIA.KIT\WallPap 1.0\unzDll.dll; DestDir: {app}
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: {app}\WallPepper.url; Section: InternetShortcut; Key: URL; String: http://www.tetram.info; Tasks: interneticon

[Icons]
Name: {group}\WallPepper; Filename: {app}\WallPepper.exe; IconIndex: 0
Name: {group}\WallPepper on the Web; Filename: {app}\WallPepper.url; Tasks: interneticon
Name: {group}\Désinstaller WallPepper; Filename: {uninstallexe}
Name: {userdesktop}\WallPepper; Filename: {app}\WallPepper.exe; Tasks: desktopicon; IconIndex: 0

[Run]
Filename: {app}\WallPepper.exe; Description: Lancer WallPepper; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: files; Name: {app}\WallPepper.url
Type: files; Name: {app}\WallPepper.ini
Type: files; Name: {app}\WallPepper.except
Type: files; Name: {app}\WallPepper.log

[LangOptions]
LanguageName=French
LanguageID=$040C

[_ISTool]
UseAbsolutePaths=false

[Code]
var
  CanUpdate, IsUpdate: Boolean;
  Textes, Valeurs: TArrayOfString;

procedure InitializeWizard();
var
  CurVersion: string;
begin
  SetArrayLength(Textes, 2);
  Textes[0] := 'Mettre à jour la version existante';
  Textes[1] := 'Installer de nouveau';
  SetArrayLength(Valeurs, 2);
  Valeurs[0] := '1';
  Valeurs[1] := '0';
  IsUpdate := False;
  CanUpdate := RegQueryStringValue(HKLM, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\{5AED0247-C16B-4DFC-B7FD-42CCE72A4F89}_is1', 'DisplayVersion', CurVersion) and (CurVersion <> '');
end;

function ScriptDlgPages(CurPage: Integer; BackClicked: Boolean): Boolean;
var
  Next: Boolean;
begin
  if CanUpdate and (
        (not BackClicked and (CurPage = wpLicense))
     or (BackClicked and ((CurPage = wpSelectTasks) or (CurPage = wpSelectDir)))
                    ) then begin
    ScriptDlgPageOpen();
    ScriptDlgPageSetCaption('WallPepper est déjà installé');
    ScriptDlgPageSetSubCaption1('Le programme d''installation à détecter que WallPepper est déjà installé sur cet ordinateur.');
    ScriptDlgPageSetSubCaption2('Selectionner l''action que vous voulez effectuer et cliquer sur Suivant pour continuer.'#13#10#13#10#13#10'ATTENTION: Si vous avez personnalisé le WebServer, pensez à sauvegarder votre répertoire WebServer avant de procéder à la mise à jour: son contenu sera remplacé.');

    Next := InputOptionArray(Textes, Valeurs, True, False);
    while Next and (Valeurs[0] + Valeurs[1] = '00') do begin
      MsgBox('Vous devez sélectionner une action.', mbError, MB_OK);
      Next := InputOptionArray(Textes, Valeurs, True, False);
    end;
    { See NextButtonClick and BackButtonClick: return True if the click should be allowed }
    IsUpdate := Valeurs[0] = '1';
    if not BackClicked then
      Result := Next
    else
      Result := not Next;
    { Close the wizard page. Do a FullRestore only if the click (see above) is not allowed }
    ScriptDlgPageClose(not Result);
  end else begin
    Result := True;
  end;
end;

function NextButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, False);
end;

function BackButtonClick(CurPage: Integer): Boolean;
begin
  Result := ScriptDlgPages(CurPage, True);
end;

function SkipCurPage(CurPage: Integer): Boolean;
begin
  Result := ((CurPage = wpSelectDir) or (CurPage = wpSelectProgramGroup)) and CanUpdate and IsUpdate;
end;
[Dirs]
Name: {app}\Plugins; Flags: uninsalwaysuninstall
